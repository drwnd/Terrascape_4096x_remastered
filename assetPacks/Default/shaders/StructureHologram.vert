#version 460 core

out vec3 texturePosition;
out vec3 voxelPosition;
out vec2 trianglePos;
flat out vec3 normal;
flat out int textureData;

struct Vertex {
    int x, y, z;
    int textureData;
};

layout (std430, binding = 0) restrict readonly buffer vertexBuffer {
    Vertex[] vertices;
};

uniform mat4 projectionViewMatrix;
uniform mat4 modelMatrix;
uniform int sideTransform[6];
uniform ivec3 iCameraPosition;
uniform ivec3 instanceCount;
uniform ivec3 instanceSize;
uniform ivec3 startPosition;

const vec3[6] NORMALS = vec3[6](vec3(0, 0, 1), vec3(0, 1, 0), vec3(1, 0, 0), vec3(0, 0, -1), vec3(0, -1, 0), vec3(-1, 0, 0));
const vec2[3] FACE_POSITIONS = vec2[3](vec2(0, 0), vec2(0, 2), vec2(2, 0));
const int NORTH = 0;
const int TOP = 1;
const int WEST = 2;
const int SOUTH = 3;
const int BOTTOM = 4;
const int EAST = 5;

vec3 getFacePositions(int side, int currentVertexId, int faceSize1, int faceSize2) {
    vec3 currentVertexOffset = vec3(FACE_POSITIONS[currentVertexId].xy, 1);

    switch (side) {
        case NORTH: return currentVertexOffset.yxz * vec3(faceSize2, faceSize1, 1);
        case TOP: return currentVertexOffset.xzy * vec3(faceSize1, 1, faceSize2);
        case WEST: return currentVertexOffset.zyx * vec3(1, faceSize1, faceSize2);
        case SOUTH: return currentVertexOffset.xyz * vec3(faceSize2, faceSize1, 0);
        case BOTTOM: return currentVertexOffset.yzx * vec3(faceSize1, 0, faceSize2);
        case EAST: return currentVertexOffset.zxy * vec3(0, faceSize1, faceSize2);
    }

    return vec3(0, 0, 0);
}

void main() {
    Vertex currentVertex = vertices[gl_VertexID / 3];
    int currentVertexId = gl_VertexID % 3;

    int countX = instanceCount.x;
    int countY = instanceCount.y;
    int countZ = instanceCount.z;

    int offsetX = (gl_InstanceID % countX);
    int offsetY = (gl_InstanceID / countX % countY);
    int offsetZ = (gl_InstanceID / countX / countY % countZ);

    int x = currentVertex.x;
    int y = currentVertex.y;
    int z = currentVertex.z;
    int side = currentVertex.textureData >> 8 & 7;

    int faceSize1 = (currentVertex.textureData >> 17 & 63) + 1;
    int faceSize2 = (currentVertex.textureData >> 11 & 63) + 1;
    vec3 inChunkPosition = getFacePositions(side, currentVertexId, faceSize1, faceSize2);
    vec4 transformedInChunkPosition = modelMatrix * vec4(ivec3(x, y, z) + inChunkPosition, 1);
    texturePosition = ivec3(offsetX, offsetY, offsetZ) * instanceSize + startPosition - iCameraPosition + transformedInChunkPosition.xyz;
    voxelPosition = texturePosition;
    normal = NORMALS[sideTransform[side]];
    texturePosition += normal * 0.001;

    gl_Position = projectionViewMatrix * vec4(texturePosition, 1.0);

    textureData = currentVertex.textureData & 0xFFFFF8FF | sideTransform[side] << 8;
    trianglePos = FACE_POSITIONS[currentVertexId];
}