#version 460 core
const int HEAD_UNDER_WATER_BIT = 1;
const int DO_SHADOW_MAPPING_BIT = 2;
const int DO_GLASS_SHADOWS_BIT = 4;

uniform sampler2D image;
uniform sampler2D shadowMap;
uniform sampler2D shadowColor;
uniform mat4 sunMatrix;

uniform int flags;
uniform float nightBrightness;
uniform float time;
uniform vec3 sunDirection;
uniform vec3 cameraPosition;

in vec3 voxelPosition;
in vec2 fragTextureCoordinate;

layout (location = 0) out vec4 fragColor;
layout (location = 1) out ivec4 intPos;

int isFlag(int bit) {
    return int((flags & bit) != 0);
}

float easeInOutQuart(float x) {
    float inValue = 8.0 * x * x * x * x;
    float outValue = 1.0 - pow(-2.0 * x + 2.0, 4.0) * 0.5;
    return step(inValue, 0.5) * inValue + step(0.5, outValue) * outValue;
}

vec3 getLightColor(vec2 shadowCoord) {
    if (isFlag(DO_GLASS_SHADOWS_BIT) == 0) return vec3(1.0);
    return max(texture(shadowColor, shadowCoord).rgb, vec3(0.5));
}

vec3 getSkyLight(vec3 position, vec3 normal) {
    if (isFlag(DO_SHADOW_MAPPING_BIT) == 0) return vec3(1.0);
    vec4 shadowCoord = sunMatrix * vec4(floor(position + normal * 0.5), 1);
    shadowCoord.xyz /= shadowCoord.w;
    shadowCoord.xy = shadowCoord.xy * 0.5 + 0.5;

    float closestDepth = texture(shadowMap, shadowCoord.xy).r;
    if (closestDepth == 0.0) return getLightColor(shadowCoord.xy);
    float currentDepth = shadowCoord.z;
    float bias = max(0.005 * (1.0 - dot(normal, sunDirection)), 0.005);

    return currentDepth + bias < closestDepth ? vec3(0.5) : getLightColor(shadowCoord.xy);
}

vec3 getColor(vec3 color) {
    float absTime = abs(time);
    float timeLight = max(nightBrightness, easeInOutQuart(absTime));
    float nightLight = 0.6 * (1 - absTime) * (1 - absTime);
    float distance = length(cameraPosition - voxelPosition);
    float waterFogMultiplier = min(1, isFlag(HEAD_UNDER_WATER_BIT) * max(0.5, distance * 0.000625));
    float fogMultiplier = 1 - exp(-distance * 0.000005);

    vec3 nightLightVec = vec3(nightLight, nightLight, nightLight);
    vec3 nightBrightnessVec = vec3(nightBrightness);
    vec3 skyLight = getSkyLight(voxelPosition, vec3(0, 0, 0));
    vec3 sunIllumination = 0.2 * absTime * skyLight;
    vec3 light = max(nightBrightnessVec, skyLight) * timeLight + sunIllumination;

    light = max(nightBrightnessVec, light);
    light.b = max(nightBrightness, max(nightBrightness, skyLight.b + nightLight) * timeLight + sunIllumination.b);

    vec3 baseColor = color * light * (1 - waterFogMultiplier) * (1 - fogMultiplier);
    vec3 waterColor = vec3(0.0, 0.098, 0.643) * waterFogMultiplier * timeLight;
    vec3 fogColor = vec3(0.46, 0.63, 0.79) * fogMultiplier * timeLight;

    return baseColor + waterColor + fogColor;
}

void main() {
    vec4 color = texture(image, fragTextureCoordinate);
    if (color.a == 0.0) discard;
    fragColor = vec4(getColor(color.rgb), 1);
    intPos = ivec4(floor(voxelPosition), 7);
}